#!/usr/bin/env maiascript

///
/// @license
/// Copyright 2020 Roberto Luiz Souza Monteiro,
///                Renata Souza Barreto,
///                Hernane Borges de Barros Pereira.
///
/// Licensed under the Apache License, Version 2.0 (the "License")
/// you may not use this file except in compliance with the License.
/// You may obtain a copy of the License at
///
///   http://www.apache.org/licenses/LICENSE-2.0
///
/// Unless required by applicable law or agreed to in writing, software
/// distributed under the License is distributed on an "AS IS" BASIS,
/// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, eitherMath.express or implied.
/// See the License for the specific language governing permissions and
/// limitations under the License.
///

///
/// mkPlural core class.
/// @namespace mkPlural
///
namespace mkPlural {
    appTitle = "MkPlural"
    appName = "MkPlural"

    ///
    /// Put a word in the plural.
    /// @method toPlural
    /// @memberof mkPlural
    /// @param {string}   word - The word that will go into the plural.
    /// @return {string}  Returns the word in the plural.
    ///
    function toPlural(word) {
        if (word.length >= 2) {
            last2chars = word.substr(word.length - 2, word.length - 1)
            lastChar = word[word.length - 1]
            // Word ends in s, ss, sh, ch, x or z:
            if (last2chars == "ss") {
                wordPlural = word + "es"
            } elseif (last2chars == "sh") {
                wordPlural = word + "es"
            } elseif (last2chars == "ch") {
                wordPlural = word + "es"
            } else {
                if ((lastChar == "s") || (lastChar == "z")) {
                    if (last2chars == "us") {
                        if (word.length == 3) {
                            wordPlural = word + "es"
                        } else {
                            wordPlural = word.substr(0, word.length - 2) + "i"
                        }
                    } elseif (last2chars == "is") {
                        wordPlural = word.substr(0, word.length - 2) + "es"
                    } else {
                        if (word == "fez") {
                            wordPlural = "fezzes"
                        } elseif (word == "gas") {
                            wordPlural = "gasses"
                        } elseif (word == "series") {
                            wordPlural = "series"
                        } elseif (word == "species") {
                            wordPlural = "species"
                        } else {
                            wordPlural = word + "es"
                        }
                    }
                } elseif (lastChar == "x") {
                    wordPlural = word + "es"
                } else {
                    if (last2chars == "fe") {
                        wordPlural = word.substr(0, word.length - 2) + "ves"
                    } elseif (lastChar == "f") {
                        if (word == "roof") {
                            wordPlural = "roofs"
                        } elseif (word == "belief") {
                            wordPlural = "beliefs"
                        } elseif (word == "chef") {
                            wordPlural = "chefs"
                        } elseif (word == "chief") {
                            wordPlural = "chiefs"
                        } else {
                            wordPlural = word.substr(0, word.length - 1) + "ves"
                        }
                    } else {
                        if (lastChar == "y") {
                            if (core.includes("aeiou", last2chars[0])) {
                                wordPlural = word + "s"
                            } else {
                                wordPlural = word.substr(0, word.length - 1) + "ies"
                            }
                        } else {
                            if (lastChar == "o") {
                                if (core.includes(["photo", "piano", "halo"], word)) {
                                    wordPlural = word + "s"
                                } else {
                                    wordPlural = word + "es"
                                }
                            } else {
                                if (last2chars == "on") {
                                    if (word == "person") {
                                        wordPlural = "people"
                                    } else {
                                        wordPlural = word.substr(0, word.length - 2) + "a"
                                    }
                                } else {
                                    if (word == "sheep") {
                                        wordPlural = "sheep"
                                    } elseif (word == "deer") {
                                        wordPlural = "deer"
                                    } elseif (word == "child") {
                                        wordPlural = "children"
                                    } elseif (word == "goose") {
                                        wordPlural = "geese"
                                    } elseif (word == "man") {
                                        wordPlural = "men"
                                    } elseif (word == "woman") {
                                        wordPlural = "women"
                                    } elseif (word == "tooth") {
                                        wordPlural = "teeth"
                                    } elseif (word == "foot") {
                                        wordPlural = "feet"
                                    } elseif (word == "mouse") {
                                        wordPlural = "mice"
                                    } else {
                                        wordPlural = word + "s"
                                    }
                                }
                            }
                        }
                    }
                }
            }
        } else {
            wordPlural = word
        }
        
        return(wordPlural)
    }
    
    // Interpret the options passed on the command line,
    // process the files and generate the requested reports.
    function run() {
        // Supports only the Node.js interpreter.
        if (core.type(process) != "undefined") {
            command = "node"
            
            fs = system.require("fs")
            readTextFile = fs.readFileSync
            
            // Read file callback.
            function readFile(input) {
                content = readTextFile(input, "utf-8")
                return(content)
            }
            
            // Command line options.
            inputFile = ""
            outputFile = ""
            
            // Get command line arguments.
            if (system.argc > 1) {
                i = 1
                while (i < system.argc) {
                    if ((system.argv[i] == "-h") | (system.argv[i] == "--help")) {
                        system.log(this.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + this.appName + " [options] [input file] [--] [arguments]")
                        system.log("Options:")
                        system.log("-h     --help               Displays this help message;")
                        system.log("-o     [output file]        Output file name.")
                        process.exit(0)
                    } elseif (system.argv[i] == "-o") {
                        i = i + 1
                        outputFile = system.argv[i]
                    } else {
                        inputFile = system.argv[i]
                        break
                    }
                    i = i + 1
                }

                if (inputFile != "") {
                    local Glob = system.require("glob")
                    // Process each file based on glob pattern.
                    function processFiles(er, files) {
                        if (files.length == 0) {
                            system.log(this.appTitle + " Command Line Interface (CLI)")
                            system.log("Usage: " + this.appName + " [options] [input file] [--] [arguments]")
                        } else {
                            for (i = 0; i < files.length; i = i + 1) {
                                // Get the full file name.
                                file = files[i]
                                
                                // Get the file without extension.
                                fileName = file.split(".")
                                fileName = fileName.shift()

                                // Get the file name extension.
                                fileExtension = file.split(".")
                                fileExtension = fileExtension.pop()

                                // Read the file contents.
                                fileContents = readFile(String(file))

                                fileLines = core.split(fileContents, "\r\n")

                                outputFileContents = "[noun plural]\n"

                                for (i = 1; i < fileLines.length; i = i + 1) {
                                    lineContents = core.trim(fileLines[i])
                                    
                                    wordPlural = mkPlural.toPlural(lineContents)

                                    if (wordPlural.length > 0) {
                                        outputFileContents = outputFileContents + wordPlural + "\n"
                                    }
                                }

                                // Do something...

                                // Sets the output file.
                                if (outputFile == "") {
                                    outputFile = fileName + ".out"
                                }

                                // Write something to the output file.
                                function errorCallback(err) {
                                    if (err) {
                                        throw(err)
                                    }
                                }
                                fs.writeFile(outputFile, outputFileContents, errorCallback)
                            }
                        }
                    }

                    options = []

                    // Get all file names based on glob pattern.
                    local glob := Glob(inputFile, options, processFiles)
                } else {
                    system.log(this.appTitle + " Command Line Interface (CLI)")
                    system.log("Usage: " + this.appName + " [options] [input file] [--] [arguments]")
                }
            } else {
                system.log("Do something...")
            }
        }
    }
}

// Run the mkPlural code if this script has been invoked
// from the command line.
if (core.type(process) != "undefined") {
    mkPlural.run()
}